# 3d flow around a cube  with read_grid and read_surf
# testing custom attributes

seed	    	    12345

#global              gridcut -1.0 comm/sort yes
global              gridcut 0.0 comm/sort yes
#global              gridcut 2.0 comm/sort yes

#global              surfs explicit/all
global              surfs explicit/distributed

boundary	    or r r

create_box  	    0 10 0 10 0 10

read_grid           data.cube.grid &
                    custom ivec int 0 &
                    custom iarray int 2 &
                    custom dvec float 0 &
                    custom darray float 3

balance_grid        rcb cell

global		    nrho 1.0 fnum 0.1

species		    air.species N O
mixture		    air N O vstream 100.0 0 0 

read_surf	    data.cube.custom &
                    custom ivec int 0 &
                    custom iarray int 2 &
                    custom dvec float 0 &
                    custom darray float 3 &
                    trans 4.5 4.5 4.5 origin 5 5 5 &
                    scale 3 3 3 rotate 45 1 1 1

surf_collide	    1 diffuse 300.0 0.0
surf_modify         all collide 1

collide             vss air air.vss

fix		    in emit/face air xlo twopass 

timestep 	    0.0001

# output

dump                10 image all 100 tmp.cube.read.image.*.ppm &
                    type type pdiam 0.1 &
                    surf proc 0.01 size 512 512 axes yes 0.9 0.02 zoom 1.2
dump_modify	    10 pad 4

dump          1 surf all 100 tmp.cube.read.dump.surf id type &
              s_ivec s_iarray[1] s_iarray[2] &
              s_dvec s_darray[1] s_darray[2] s_darray[3]

dump          2 grid all 100 tmp.cube.read.dump.grid id &
              g_ivec g_iarray[1] g_iarray[2] &
              g_dvec g_darray[1] g_darray[2] g_darray[3]

write_surf      tmp.cube.read.data.surf type &
                custom ivec custom iarray custom dvec custom darray &
                points no

write_grid      tmp.cube.read.data.grid &
                custom ivec custom iarray custom dvec custom darray

# first run

fix             1 balance 100 1.01 rcb part flip yes

stats		    50
stats_style	    step cpu np nattempt ncoll nscoll nscheck f_1[*]

run 		    500

# setup up per-particle custom attributes

variable        pivec particle id/100000
variable        piarray1 particle p_ivec+1
variable        piarray2 particle v_piarray1+1
variable        pdvec particle id/100000+3.5
variable        pdarray1 particle p_dvec+1
variable        pdarray2 particle v_pdarray1+1
variable        pdarray3 particle v_pdarray2+1

custom          particle p_ivec set v_pivec all NULL type int
custom          particle p_iarray[1] set v_piarray1 all NULL type int size 2
custom          particle p_iarray[2] set v_piarray2 all NULL type int size 2
custom          particle p_dvec set v_pdvec all NULL
custom          particle p_darray[1] set v_pdarray1 all NULL size 3
custom          particle p_darray[2] set v_pdarray2 all NULL size 3
custom          particle p_darray[3] set v_pdarray3 all NULL size 3

dump          3 particle all 100 tmp.step.read.dump.particle id x y &
              p_ivec p_iarray[1] p_iarray[2] &
              p_dvec p_darray[1] p_darray[2] p_darray[3]

# second run

compute       1 reduce ave p_ivec p_iarray[1] p_iarray[1] &
              p_dvec p_darray[1] p_darray[2] p_darray[3]
compute       2 reduce ave g_ivec g_iarray[1] g_iarray[1] &
              g_dvec g_darray[1] g_darray[2] g_darray[3]
compute       3 reduce ave s_ivec s_iarray[1] s_iarray[1] &
              s_dvec s_darray[1] s_darray[2] s_darray[3]

stats_style	step cpu np nattempt ncoll nscoll nscheck f_1[*] &
                c_1[*] c_2[*] c_3[*]

balance_grid    rcb part flip yes

run 		    500

write_surf      tmp.cube.read.data.surf2 type &
                custom ivec custom iarray custom dvec custom darray &
                points no

write_grid      tmp.cube.read.data.grid2 &
                custom ivec custom iarray custom dvec custom darray
                #points no

#write_restart   tmp.cube.read.restart
write_restart   tmp.cube.read.restart.% fileper 2

# remove all custom surfs

undump         1
undump         2
undump         3

custom         surf s_ivec remove
custom         surf s_iarray remove
custom         surf s_dvec remove
custom         surf s_darray remove

custom         grid g_ivec remove
custom         grid g_iarray remove
custom         grid g_dvec remove
custom         grid g_darray remove

custom         particle p_ivec remove
custom         particle p_iarray remove
custom         particle p_dvec remove
custom         particle p_darray remove

# third run

uncompute       1
uncompute       2
uncompute       3
stats_style	step cpu np nattempt ncoll nscoll nscheck f_1[*]

run            500
