################################################################################
# thermal gas in a 3d box with collisions
# the fix temp/rescale command is used to ramp and cool the temperature
# the compute thermal/grid command is used to measure the temperature

# Note:
#  - The "comm/sort” option to the “global” command is used to match MPI runs.
#  - The “twopass” option is used to match Kokkos runs.
# The "comm/sort" and "twopass" options should not be used for production runs.
################################################################################

seed	    	    12345
dimension   	    3
global              gridcut 1.0e-5 comm/sort yes

boundary	    rr rr rr

create_box  	    0 0.0001 0 0.0001 0 0.0001
create_grid 	    10 10 10

balance_grid        rcb part

species		    ar.species Ar
mixture		    air Ar vstream 0.0 0.0 0.0 temp 300.0

global              nrho 7.07043E22
global              fnum 7.07043E6

collide		    vss air ar.vss

create_particles    air n 10000 twopass

compute             1 thermal/grid all air temp
compute             2 reduce ave c_1[1]

stats		    100
compute             temp temp
stats_style	    step cpu np nattempt ncoll c_temp c_2

timestep 	    7.00E-9

fix                 tstat temp/rescale 200 300 600 ave yes
run 		    1000

fix                 tstat temp/rescale 200 600 100 ave yes
run 		    1000
